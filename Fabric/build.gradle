plugins {
    id 'fabric-loom' version '1.9.+'
    id 'idea'
    id 'io.github.CDAGaming.cursegradle' version '1.6.1'
    id "com.modrinth.minotaur" version "2.+"
}

archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${mappings_version}@zip")
    }

    modCompileOnly "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modLocalRuntime "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modCompileOnly "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modLocalRuntime "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    compileOnly project(":Common")

    modImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}"
    include "maven.modrinth:midnightlib:${project.midnightlib_version}"
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }

    mixin {
        defaultRefmapName.convention("${mod_id}.refmap.json")
    }

    accessWidenerPath = file("${project(':Common').projectDir.path}/src/main/resources/tslatentitystatus.accesswidener")
}

processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

sourcesJar {
    from(project(":Common").sourceSets.main.allSource)
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'              : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : minecraft_version,
                "MixinConfigs": "${mod_id}.mixins.json"
        ])
    }
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = compileJava.destinationDirectory.asFile.get() as File
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
        }
    }
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN")

    project {
        id = curseforge_id
        changelog = 'https://github.com/Tslat/TslatEntityStatus/commits/1.20'
        releaseType = 'release'
        addGameVersion minecraft_version

        mainArtifact(remapJar) {
            it.displayName = "${project.mod_name} Fabric ${project.minecraft_version}-${project.version}"
        }
    }
}

modrinth {
    token = System.getenv("modrinthKey")
    projectId = mod_id
    versionNumber = project.version
    versionType = "release"
    uploadFile = remapJar
    gameVersions = ["${project.minecraft_version}"]
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
        optional.version "midnightlib", "1.5.0-fabric"
    }
}