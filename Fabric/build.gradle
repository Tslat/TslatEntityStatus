plugins {
    id 'ml-platform-config'
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'net.darkhax.curseforgegradle' version '1.1.15'
    id "com.modrinth.minotaur" version "2.+"
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${mappings_mc_version}:${parchment_version}@zip")
    }
	
    modCompileOnly "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modCompileOnly "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"
    modLocalRuntime "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modLocalRuntime "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    
    include(modImplementation("maven.modrinth:midnightlib:${midnightlib_version}"))
}

loom {
    def accessWidenerFile = project(":common").file("src/main/resources/${mod_id}.accesswidener")
	
    if (accessWidenerFile.exists())
        accessWidenerPath.set(accessWidenerFile)

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
	
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("runs/client")
        }
		
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("runs/server")
        }
    }
}

task publishToCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    group = "publishing"
    apiToken = System.getenv("CURSEFORGE_TOKEN") ?: "Invalid/No API Token Found"

    def mainFile = upload(curseforge_id, tasks.remapJar)
    mainFile.releaseType = "release"
    mainFile.addModLoader("Fabric")
    mainFile.addGameVersion(minecraft_version)
    mainFile.addJavaVersion("Java ${java_version}")
    mainFile.changelog = "${changelog_url}"
    mainFile.displayName = "${mod_name} ${project.name[0].toUpperCase() + project.name[1..-1]} ${minecraft_version}-${project.version}"

    //https://github.com/Darkhax/CurseForgeGradle#available-properties
}

modrinth {
    token = System.getenv("modrinthKey")
    projectId = mod_id
    versionNumber = project.version
    versionType = "release"
    uploadFile = remapJar
    gameVersions = ["${minecraft_version}"]
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
        optional.version "midnightlib", "${midnightlib_version}"
    }
}